#!/usr/bin/env groovy

@Library('apm@current') _

pipeline {
  agent { label 'ubuntu && immutable' }
  environment {
    BASE_DIR="src/github.com/elastic/package-storage"
    DOCKER_REGISTRY = 'docker.elastic.co'
    DOCKER_REGISTRY_SECRET = 'secret/apm-team/ci/docker-registry/prod'
    PIPELINE_LOG_LEVEL='INFO'
  }
  options {
    timeout(time: 1, unit: 'HOURS')
    buildDiscarder(logRotator(numToKeepStr: '20', artifactNumToKeepStr: '20', daysToKeepStr: '30'))
    timestamps()
    ansiColor('xterm')
    disableResume()
    durabilityHint('PERFORMANCE_OPTIMIZED')
    rateLimitBuilds(throttle: [count: 60, durationName: 'hour', userBoost: true])
    quietPeriod(10)
  }
  triggers {
    issueCommentTrigger('(?i).*(?:jenkins\\W+)?run\\W+(?:the\\W+)?tests(?:\\W+please)?.*')
  }
  stages {
    /**
     Checkout the code and stash it, to use it on other stages.
     */
    stage('Checkout') {
      steps {
        deleteDir()
        gitCheckout(basedir: "${BASE_DIR}")
        stash allowEmpty: true, name: 'source', useDefaultExcludes: false
      }
    }
    /**
     Builds and validates the packages
     */
    stage('Build') {
      steps {
        deleteDir()
        unstash 'source'
        dir("${BASE_DIR}"){
          insideGo(){
            sh(label: 'Checks if the packages can be build and are valid',script: 'mage -debug build')
          }
        }
      }
    }
    stage('Docker image'){
      environment {
        DOCKER_IMAGE = "observability-ci/package-registry"
        DOCKER_TAG = "experimental"
        DOCKER_IMAGE_NAME = "${env.DOCKER_REGISTRY}/${env.DOCKER_IMAGE}:${env.DOCKER_TAG}"
        DOCKER_IMAGE_NAME_SHA1 = "${env.DOCKER_REGISTRY}/${env.DOCKER_IMAGE}:${env.GIT_BASE_COMMIT}"
      }
      when {
        anyOf {
          branch 'master'
          branch "\\d+\\.\\d+"
          branch "v\\d?"
          tag "v\\d+\\.\\d+\\.\\d+*"
        }
      }
      steps {
        dockerLogin(secret: "${env.DOCKER_REGISTRY_SECRET}", registry: "${env.DOCKER_REGISTRY}")
        dir("${BASE_DIR}/dev"){
          retry(3){
            sh(label:'Build and push', script: """
              docker buil -t ${env.DOCKER_IMAGE_NAME}
              docker push ${env.DOCKER_IMAGE_NAME}
              docker tag ${env.DOCKER_IMAGE_NAME} ${env.DOCKER_IMAGE_NAME_SHA1}
              docker push ${env.DOCKER_IMAGE_NAME_SHA1}
            """)
          }
        }
      }
    }
  }
  post {
    cleanup {
      notifyBuildResult(prComment: true)
    }
  }
}

def insideGo(Closure body){
  def goAgent = docker.build("go-agent", ".ci/jenkins-go-agent")
  goAgent.inside("-v ${env.WORKSPACE}/src:/go/src"){
    withEnv([
      "HOME=${env.WORKSPACE}/${BASE_DIR}",
      "GOPATH=/go",
    ]) {
      sh(label: 'Go version', script: 'go version')
      sh(label: 'Install Mage', script: '''
        go get -u github.com/magefile/mage 
        mage -version
      ''')
      body()
    }
  }
}
